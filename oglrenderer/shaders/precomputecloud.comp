#version 450 core
#define GLSL_SHADER
#extension GL_EXT_scalar_block_layout : require

#include "deviceconstants.h"
#include "devicestructs.h"
#include "cloud.h"

layout(local_size_x = PRECOMPUTE_CLOUD_LOCAL_SIZE, local_size_y = PRECOMPUTE_CLOUD_LOCAL_SIZE, local_size_z = PRECOMPUTE_CLOUD_LOCAL_SIZE) in;
layout(std430, binding = RENDERER_PARAMS) uniform RendererParamsUniform
{
	RendererParams renderParams;
};
layout(std430, binding = FBM_PARAMS) uniform FBMParamsUniform
{
	NoiseParams fbmParams;
};
layout(std430, binding = PERLIN_PARAMS) uniform PerlinParamsUniform
{
	NoiseParams perlinParams;
};
layout(std430, binding = WORLEY_PARAMS) uniform WorleyParamsUniform
{
	NoiseParams worleyParams;
};
layout (binding = PRECOMPUTE_CLOUD_CLOUD_TEX, rgba32f) uniform image3D cloudTexture;

void main()
{
	if (gl_GlobalInvocationID.x > 255 || gl_GlobalInvocationID.y > 255 || gl_GlobalInvocationID.z > 255)
	{
		return;
	}

    vec3 uvw = gl_GlobalInvocationID.xyz / vec3(CLOUD_RESOLUTION, CLOUD_RESOLUTION, CLOUD_RESOLUTION);

    const int count = renderParams.mScreenSettings.z % 4;
    vec4 color = imageLoad(cloudTexture, ivec3(gl_GlobalInvocationID.xyz));
	switch (count)
	{
		case 0:	
		{
			color.x = perlinWorley3D(uvw, renderParams.mSettings.x * renderParams.mCloudSettings.y, perlinParams.mSettings.z, perlinParams.mNoiseOctaves, true); 

			break;
		}
		case 1:	
		{
			color.y = worley3D(uvw * perlinParams.mSettings.z, renderParams.mSettings.x * renderParams.mCloudSettings.y * perlinParams.mSettings.z, true); 
			break;
		}
		case 2:	
		{
			color.z = worley3D(uvw * 2 * perlinParams.mSettings.z, renderParams.mSettings.x * 2 * renderParams.mCloudSettings.y * perlinParams.mSettings.z, true); 
			break;
		}
		case 3: 
		{
			color.w = worley3D(uvw * 4 * perlinParams.mSettings.z, renderParams.mSettings.x * 4 * renderParams.mCloudSettings.y * perlinParams.mSettings.z, true); 
			break;
		}
	}
    imageStore(cloudTexture, ivec3(gl_GlobalInvocationID.xyz), color);
}