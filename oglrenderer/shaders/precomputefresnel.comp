#version 450 core
#define GLSL_SHADER
#extension GL_EXT_scalar_block_layout : require

#include "bsdf.h"
#include "deviceconstants.h"
#include "devicestructs.h"

layout(local_size_x = PRECOMPUTE_FRESNEL_LOCAL_SIZE, local_size_y = PRECOMPUTE_FRESNEL_LOCAL_SIZE) in;

layout(std430, binding = RENDERER_PARAMS) uniform RendererParamsUniform
{
    RendererParams renderParams;
};

layout(binding = PRECOMPUTE_FRESNEL_TEX, rgba32f) uniform image2D fresnelTex;

// A single iteration of Bob Jenkins' One-At-A-Time hashing algorithm.
uint hash(uint x) {
    x += (x << 10u);
    x ^= (x >> 6u);
    x += (x << 3u);
    x ^= (x >> 11u);
    x += (x << 15u);
    return x;
}

// Compound versions of the hashing algorithm I whipped together.
uint hash(uvec2 v) { return hash(v.x ^ hash(v.y)); }
uint hash(uvec3 v) { return hash(v.x ^ hash(v.y) ^ hash(v.z)); }
uint hash(uvec4 v) { return hash(v.x ^ hash(v.y) ^ hash(v.z) ^ hash(v.w)); }


// Construct a float with half-open range [0:1] using low 23 bits.
// All zeroes yields 0.0, all ones yields the next smallest representable value below 1.0.
float floatConstruct(uint m) {
    const uint ieeeMantissa = 0x007FFFFFu; // binary32 mantissa bitmask
    const uint ieeeOne = 0x3F800000u; // 1.0 in IEEE binary32

    m &= ieeeMantissa;                     // Keep only mantissa bits (fractional part)
    m |= ieeeOne;                          // Add fractional part to 1.0

    float  f = uintBitsToFloat(m);       // Range [1:2]
    return f - 1.0;                        // Range [0:1]
}

// Pseudo-random value in half-open range [0:1].
float random(float x) { return floatConstruct(hash(floatBitsToUint(x))); }
float random(vec2  v) { return floatConstruct(hash(floatBitsToUint(v))); }
float random(vec3  v) { return floatConstruct(hash(floatBitsToUint(v))); }
float random(vec4  v) { return floatConstruct(hash(floatBitsToUint(v))); }

void main()
{
    const vec2 uv = gl_GlobalInvocationID.xy / 100.0f;

    const float roughness = uv.x;
    const float roughness2 = roughness * roughness;
    const float cosTheta = uv.y;
    const float theta = acos(cosTheta);

    const vec3 wo = normalize(vec3(sin(theta), 0, cosTheta));
    float albedo = 0.0f;
    for (int i = 0; i < SAMPLE_COUNT; i++)
    {
        // random variables
        vec4 inputs = vec4(vec3(uv, i), renderParams.mSettings.x);
        vec4 inputs2 = vec4(vec3(uv, i+1), renderParams.mSettings.x);
        vec2 u = vec2(random(inputs), random(inputs2));

        // Compute the half vector and incoming light direction.
        float alpha = roughness2;
        vec3 wh = GGX_sample(u, wo, vec2(roughness));
        vec3 wi = -reflect(wo, wh);

        // Compute dot products for this sample.
        float nDotL = clamp(wi.z, 0.0f, 1.0);
        float oDotH = clamp(dot(wo, wh), 0.0f, 1.0);

        // Compute the Fresnel term.
        float Fc = fresnel(1.5f, oDotH);

        // Compute the sample weight, combining the geometric term, BRDF denominator, and PDF.
        // https://hal.inria.fr/hal-00996995v2/document, Algorithm 2
        const float g1 = G1(cosTheta, alpha);
        const float g2 = G2(nDotL, cosTheta, alpha);
        float weight = g1 == 0.0f ? 1.0f : g2 / g1;

        // Add the contribution of this sample.
        albedo += weight * Fc;
    }

    // Normalize integrated terms.
    albedo /= float(SAMPLE_COUNT);
    imageStore(fresnelTex, ivec2(gl_GlobalInvocationID.xy), vec4(vec3(albedo), 1.0f));
}